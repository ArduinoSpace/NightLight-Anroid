apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "ru.annin.nightlight"

        minSdkVersion 14
        targetSdkVersion 25

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")

        resConfigs "en", "ru"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") { // Only Release
            File outDir = file("${rootDir}/out")
            if (!outDir.exists()) { outDir.mkdirs() }
            variant.assemble.doLast {
                variant.outputs.each { output ->
                    File apkFile = output.outputFile
                    copy {
                        from apkFile
                        into outDir
                        rename { fileName -> "nightlight_v${variant.versionName}.apk" }
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Library
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha4'
    compile 'com.android.support:design:25.0.1'

    // UI
    compile 'com.pavelsikun:vintage-chroma:1.5'

    // Rx
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.github.ivbaranov:rxbluetooth:0.1.2'

    // Test's
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.0.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}